apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: project(':react-native-config').projectDir.getPath() + "/dotenv.gradle"

import com.android.build.OutputFile

project.ext.react = [
        entryFile           : "index.android.js",
        bundleInStaging     : true,
        devDisabledInStaging: true
]

apply from: '../../node_modules/react-native/react.gradle'
apply from: "../../node_modules/react-native-sentry/sentry.gradle"

def enableSeparateBuildPerCPUArchitecture = false


apply plugin: 'com.jaredsburrows.license'
licenseReport {
    copyHtmlReportToAssets = true
}

android {
    compileSdkVersion 28

    defaultConfig {
        targetSdkVersion 28
        applicationId project.env.get('APP_ID')
        minSdkVersion 21
        multiDexEnabled true
        versionCode 43
        versionName '2.8.1'

        resValue "string", "build_config_package", "com.hedvig.app"
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }

        buildConfigField("String", "DEFAULT_LANGUAGE", "\"${rootProject.ext.defaultLanguage}\"")
        buildConfigField("String[]", "EXCLUDED_LANGUAGES", "{\"${rootProject.ext.excludedLanguages.join("\",\"")}\"}")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk true
            include 'armeabi-v7a', 'x86'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        staging {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def versionCodes = ['armeabi-v7a': 1, 'x86': 2]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {
                output.versionCodeOverride = versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }

    configurations.all {
        resolutionStrategy.force 'android.arch.lifecycle:livedata-core:1.1.0'
        resolutionStrategy.force 'android.arch.core:runtime:1.1.0'
        resolutionStrategy.force 'android.arch.core:common:1.1.0'
    }

    dynamicFeatures = [":onboarding", ":logged_in", ":landing"]

}

dependencies {
    api project(':common')
    api project(':navigation')

    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    api 'nl.littlerobots.rxlint:rxlint:1.7.3'

    implementation "com.facebook.react:react-native:0.57.7"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android lifecycle
    def lifecycle_version = "1.1.0"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"


    // WorkManager
    def workmanager_version = "1.0.1"
    implementation "android.arch.work:work-runtime:$workmanager_version"
    implementation "android.arch.work:work-runtime-ktx:$workmanager_version"

    // Okhttp
    def okhttp3_version = '3.12.0'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"

    // Firebase
    api 'com.google.android.gms:play-services-base:16.1.0'
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.0'
    stagingImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.0'

    // Segment
    api 'com.google.android.gms:play-services-ads:15.0.1'
    api 'com.segment.analytics.android:analytics:4.2.6'
    // compile 'io.branch.segment.analytics.android.integrations:branch:1.0.2-RELEASE'

    api 'io.branch.sdk.android:library:3.2.0'

    // Koin
    def koin_version = "2.0.0-GA"
    api "org.koin:koin-android:$koin_version"
    api "org.koin:koin-android-viewmodel:$koin_version"

    // coroutines
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    // test
    testImplementation 'junit:junit:4.12'

    debugApi 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
}

apply plugin: 'com.google.gms.google-services'
com.google.gms.googleservices.GoogleServicesPlugin.config.disableVersionCheck = true
